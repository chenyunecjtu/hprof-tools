apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
version = '1.0'

mainClassName = "com.badoo.bmd.decruncher.BmdDecruncher"

repositories {
    mavenCentral()
}

run {
    minHeapSize = "128m"
    maxHeapSize = "1024m"
    if(project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

task jarWithDependencies(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.badoo.bmd.decruncher.BmdDecruncher'
    }
    archiveBaseName = project.name + '-all'
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = 'exclude'
    with tasks.jar
}


dependencies {
    implementation project(':hprof-lib')
    implementation project(':bmd-lib')
    implementation 'commons-io:commons-io:2.4'
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation files('libs/dex-reader-1.15.jar')
    testImplementation group: 'junit', name: 'junit', version: '4.11'
}
